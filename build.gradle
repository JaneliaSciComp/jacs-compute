plugins {
    id "com.github.hierynomus.license" version "0.16.1"
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    group = 'org.janelia.jacs2'
    version = '9.22.2'
}

project.ext {
    eclipseBuildDir = 'eclipseBuild'
}

subprojects {

    sourceSets {
        integrationTest {
            compileClasspath += sourceSets.main.output + sourceSets.test.output
            runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        }
    }

    configurations {
        provided
        implementation.extendsFrom(provided)
        testArtifacts.extendsFrom(testImplementation)
        integrationTestCompileOnly.extendsFrom(testCompileOnly)
        integrationTestImplementation.extendsFrom(testImplementation)
        integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
        integrationTestRuntimeElements.extendsFrom(integrationTestImplementation, integrationTestRuntimeOnly)
    }

    apply from: "${rootDir}/deps.gradle"

    afterEvaluate {
        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url = "https://nexus.janelia.org/repository/maven-releases"
            }
            maven {
                url = "https://nexus.janelia.org/repository/maven-snapshots"
            }
            maven {
                url = "https://maven.scijava.org/content/groups/public"
            }
            flatDir {
                dirs("${rootDir}/projectrepository")
            }
        }

        compileJava {
            doFirst {
                options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
                ]
            }
        }

        compileTestJava {
            doFirst {
                options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
                ]
            }
        }

        compileIntegrationTestJava {
            doFirst {
                options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
                ]
            }
        }

        eclipse {
            classpath {
                defaultOutputDir = file(eclipseBuildDir)
                downloadSources = true
                downloadJavadoc = true
            }
            project {
                name = "$project.name"
            }
        }
    
        idea {
            module {
                downloadSources = true
                downloadJavadoc = true
                scopes.TEST.plus += [
                        configurations.integrationTestRuntimeElements,
                ]
            }
        }

        license {
            header = rootProject.file('LICENSE')
            exclude('**/*')
        }

        downloadLicenses {
            includeProjectDependencies = true
            dependencyConfiguration = 'implementation'
        }

        java {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }

        tasks.register('testJar', Jar) {
            from sourceSets.test.output
            archiveClassifier.set("test")
        }

        tasks.register('integrationTest', Test) {
            testClassesDirs += sourceSets.integrationTest.output.classesDirs
            classpath = sourceSets.integrationTest.runtimeClasspath
        }
    
        artifacts {
            testArtifacts(testJar)
        }
    }
}

tasks.register('updateWrapper', Wrapper) {
    gradleVersion = "8.12"
}
