plugins {
    id "com.github.hierynomus.license" version "0.16.1"
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    group = 'org.janelia.jacs2'
    version = '9.17'
}

project.ext {
    eclipseBuildDir = 'eclipseBuild'
}

subprojects {
    configurations {
        provided
        implementation.extendsFrom(provided)
        testArtifacts.extendsFrom(testImplementation)
        integrationTestCompileOnly.extendsFrom(testCompileOnly)
        integrationTestImplementation.extendsFrom(testImplementation)
        integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
    }

    apply from: "${rootDir}/deps.gradle"

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationTest/java')
            }
            resources {
                srcDir file('src/integrationTest/resources')
            }
        }
    }

    afterEvaluate {
        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url "https://nexus.janelia.org/repository/maven-releases"
            }
            maven {
                url "https://nexus.janelia.org/repository/maven-snapshots"
            }
            maven {
                url "https://maven.scijava.org/content/groups/public"
            }
            flatDir {
                dirs "${rootDir}/projectrepository"
            }
        }

        compileJava {
            doFirst {
                options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
                ]
            }
        }

        compileTestJava {
            doFirst {
                options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
                ]
            }
        }

        compileIntegrationTestJava {
            doFirst {
                options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
                ]
            }
        }

        eclipse {
            classpath {
                defaultOutputDir = file(eclipseBuildDir)
                downloadSources = true
                downloadJavadoc = true
            }
            project {
                name = "$project.name"
            }
        }
    
        idea {
            module {
                downloadSources = true
                downloadJavadoc = true
                scopes.TEST.plus += [
                    configurations.integrationTestCompileClasspath,
                    configurations.integrationTestRuntimeClasspath
                ]
            }
        }

        license {
            header rootProject.file('LICENSE')
            exclude "**/*"
        }

        downloadLicenses {
            includeProjectDependencies = true
            dependencyConfiguration = 'implementation'
        }

        tasks.register('testJar', Jar) {
            from sourceSets.test.output
            archiveClassifier.set("test")
        }

        tasks.register('integrationTest', Test) {
            testClassesDirs += sourceSets.integrationTest.output.classesDirs
            classpath = sourceSets.integrationTest.runtimeClasspath
        }
    
        artifacts {
            testArtifacts testJar
        }
    }
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

}

tasks.register('updateWrapper', Wrapper) {
    gradleVersion = "8.1.1"
}
