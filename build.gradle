plugins {
    id "com.github.hierynomus.license" version "0.15.0"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    group = 'org.janelia.jacs2'
    version = '2.1.0'
}

project.ext {
    eclipseBuildDir = 'eclipseBuild'
}

subprojects {
    apply from: "${rootDir}/deps.gradle"

    afterEvaluate {
        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url "http://maven.int.janelia.org/nexus/content/groups/public/"
            }
            flatDir {
                dirs "${rootDir}/projectrepository"
            }
        }

        configurations {
            provided
            compile.extendsFrom(provided)
            integrationTestCompile.extendsFrom(testCompile)
            integrationTestRuntime.extendsFrom(testRuntime)
        }
    
    
        sourceSets {
            integrationTest {
                java {
                    compileClasspath += main.output + test.output
                    runtimeClasspath += main.output + test.output
                    srcDir file('src/integrationTest/java')
                }
                resources {
                    srcDir file('src/integrationTest/resources')
                }
            }
        }

        compileJava {
            doFirst {
                options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
                ]
            }
        }

        compileTestJava {
            doFirst {
                options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
                ]
            }
        }

        compileIntegrationTestJava {
            doFirst {
                options.compilerArgs = [
                    '-Xlint:deprecation',
                    '-Xlint:unchecked'
                ]
            }
        }

        eclipse {
            classpath {
                defaultOutputDir = file(eclipseBuildDir)
                downloadSources = true
                downloadJavadoc = true
            }
            project {
                name = "$project.name"
            }
        }
    
        idea {
            module {
                downloadSources = true
                downloadJavadoc = true
                scopes.TEST.plus += [
                    configurations.integrationTestCompile,
                    configurations.integrationTestRuntime
                ]
            }
        }

        license {
            header rootProject.file('LICENSE.md')
            exclude "**/*"
        }

        downloadLicenses {
            includeProjectDependencies = true
            dependencyConfiguration = 'compile'
        }

        task integrationTest(type: Test) {
            testClassesDirs += sourceSets.integrationTest.output.classesDirs
            classpath = sourceSets.integrationTest.runtimeClasspath
        }
    
    }
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

}

task updateWrapper(type: Wrapper) {
    gradleVersion = "5.1.1"
}
