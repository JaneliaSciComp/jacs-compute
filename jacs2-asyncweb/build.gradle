buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.netflix.nebula.ospackage:com.netflix.nebula.ospackage.gradle.plugin:11.10.1'
    }
}

plugins {
    id 'application'
    id 'distribution'
    id 'com.netflix.nebula.ospackage' version '11.10.1'
}

application {
    mainClass = 'org.janelia.jacs2.app.AsyncServicesApp'
}

project(':jacs2-asyncweb') {
    dependencies {
        implementation project(':jacs2-commonweb'),
                       WELD_CDI_SE_CORE_LIB

        runtimeOnly JERSEY_INJECT_HK2_LIB,
                    JERSEY_MEDIA_LIB,
                    JERSEY_MEDIA_JSON_LIB,
                    JERSEY_WELD_SE_LIB

        testImplementation project(path: ":jacs2-commonweb", configuration: "testArtifacts")
    }

    distributions {
        main {
           contents {
                into('swagger-webapp') {
                    from(new File(project(':jacs2-commonweb').projectDir, 'src/main/swagger-webapp')) {
                        include '**'
                    }
                    from(new File(projectDir, 'src/main/swagger-webapp')) {
                        include 'index.html'
                    }
                }
            }
        }
    }

    tasks.withType(CreateStartScripts).each { task ->
        task.doLast {
            task.windowsScript.write task.windowsScript.text.replaceFirst(/(set CLASSPATH=%APP_HOME%\\lib\\).*/, {
                "${it[1]}*"
            })
            // on unix also add a line that sets the umask for the process
            task.unixScript.write task.unixScript.text.replaceFirst(/(CLASSPATH=.APP_HOME\/lib\/).*/, {
                "CLASSPATH=\$(echo \$APP_HOME/lib/*.jar | tr ' ' ':')\numask 002"
            })
        }
    }

}
