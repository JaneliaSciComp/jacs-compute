plugins {
    id 'application'
    id 'distribution'
    id 'nebula.rpm' version '4.8.0'
}

mainClassName = 'org.janelia.jacs2.app.AsyncServicesApp'

project(":jacs2-asyncweb") {
    dependencies {
        implementation project(":jacs2-commonweb"),
                       JERSEY_SERVER_LIB,
                       WELD_CDI_SE_CORE_LIB

        testImplementation project(path: ":jacs2-commonweb", configuration: "testArtifacts")
    }

    distributions {
        main {
           contents {
                into('swagger-webapp') {
                    from(new File(project(':jacs2-commonweb').projectDir, 'src/main/swagger-webapp')) {
                        include '**'
                    }
                    from(new File(projectDir, 'src/main/swagger-webapp')) {
                        include 'index.html'
                    }
                }
            }
        }
    }

    tasks.withType(CreateStartScripts).each { task ->
        task.doLast {
            task.windowsScript.write task.windowsScript.text.replaceFirst(/(set CLASSPATH=%APP_HOME%\\lib\\).*/, {
                "${it[1]}*"
            })
            // on unix also add a line that sets the umask for the process
            task.unixScript.write task.unixScript.text.replaceFirst(/(CLASSPATH=.APP_HOME\/lib\/).*/, {
                "CLASSPATH=\$(echo \$APP_HOME/lib/*.jar | tr ' ' ':')\numask 002"
            })
        }
    }

}
